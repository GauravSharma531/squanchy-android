apply from: rootProject.file('dependencies.gradle')

configurations {
    ktlint
}

dependencies {
    ktlint "com.github.shyiko:ktlint:${ktLintVersion}"
}

task ktlint(type: JavaExec, group: 'verification') {
    description = 'Check Kotlin code style.'
    classpath = configurations.ktlint
    main = 'com.github.shyiko.ktlint.Main'
    args '--reporter=plain', "--reporter=checkstyle,output=${buildDir}/ktlint.xml", '--android', 'src/**/*.kt'
}

project.afterEvaluate {
    project.tasks['check'].dependsOn ktlint
}

task ktlintFormat(type: JavaExec, group: 'formatting') {
    description = 'Fix Kotlin code style deviations.'
    classpath = configurations.ktlint
    main = 'com.github.shyiko.ktlint.Main'
    args '-F', '--reporter=plain', '--android', 'src/**/*.kt'
}
